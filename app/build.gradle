apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "io.zelbess.grpcandroid"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        String generatedSourcePath = "${projectDir}/src/generated"
        debug { java { srcDir "${generatedSourcePath}/debug/grpc" } }
        release { java { srcDir "${generatedSourcePath}/release/grpc" } }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def reactiveGrpcVersion = "1.0.1"
def grpcVersion = '1.32.1'

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.8.0' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
        rxgrpc { artifact = "com.salesforce.servicelibs:rxgrpc:${reactiveGrpcVersion}" }
        reactor { artifact = "com.salesforce.servicelibs:reactor-grpc:${reactiveGrpcVersion}" }
    }
    clean {
        delete protobuf.generatedFilesBaseDir
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins { java { option "lite" } }
            task.plugins {
                grpc { option "lite" }
                rxgrpc {}
            }
        }
        all()*.plugins {
            grpc { }
            rxgrpc {}
            reactor {}
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'

    def koin_version='2.2.0-rc-2'
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0" //outdated rxjava2 needed by grpc :/
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "com.salesforce.servicelibs:reactive-grpc:${reactiveGrpcVersion}"
    implementation "com.salesforce.servicelibs:rxgrpc-stub:${reactiveGrpcVersion}"
    implementation "com.salesforce.servicelibs:reactor-grpc-stub:${reactiveGrpcVersion}"

    implementation 'com.google.protobuf:protobuf-javalite:3.12.0'
    implementation "io.grpc:grpc-okhttp:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf-lite:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

}

tasks.withType(GenerateModuleMetadata) {
    // Module metadata, introduced in Gradle 6.0, conflicts with our publishing task for grpc-alts and grpc-compiler.
    enabled = false
}

ext {
    protocPluginBaseName = 'protoc-gen-grpc-java'
    javaPluginPath = "$rootDir/compiler/build/exe/java_plugin/$protocPluginBaseName"

}
